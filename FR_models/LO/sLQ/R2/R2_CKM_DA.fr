(***************************************************************************************************************)
(******                                FeynRules model file for R2 Leptoquark                             ******)
(******                                           Down-aligned                                            ******)
(******                                        * Version 1.0.0 *                                          ******)
(******                                                                                                   ******)
(******                        Authors: A. Bhaskar, Y. Chaurasia, A. Das,                                 ******)
(******                                 A. Kumar, T. Mandal, S. Mitra,                                    ******)
(******                                 C. Neeraj, R. Sharma                                              ******)
(******                                                                                                   ******)
(******                                See README.md for citation information.                            ******)
(******                                                                                                   ******)
(***************************************************************************************************************)

(* ************************** *)
(* *****  Information   ***** *)
(* ************************** *)

M$ModelName = "R2_CKM_DA";

M$InteractionOrderHierarchy = {
  {QCD, 1},
  {QED, 2},
  {QLQ, 3}
};

M$Parameters = {
Y20RL == {
    	ParameterType    -> External,
	ComplexParameter -> True,
    	Indices          -> {Index[Generation], Index[Generation]},
	BlockName        -> YUKR2RL,
    	Value            -> {Y20RL[1,1] -> 0.0, Y20RL[1,2] -> 0.0, Y20RL[1,3] -> 0.0,
                      	     Y20RL[2,1] -> 0.0, Y20RL[2,2] -> 0.0, Y20RL[2,3] -> 0.0,
                             Y20RL[3,1] -> 0.0, Y20RL[3,2] -> 0.0, Y20RL[3,3] -> 0.0},
    	TeX              -> Superscript[Subscript[Y, "2"],RL],
	InteractionOrder -> {QLQ, 1},
    	Description      -> "r20 leptoquark RL Yukawa coupling matrix"
},

Y20LR == {
    	ParameterType    -> External,
	ComplexParameter -> True,
    	Indices          -> {Index[Generation], Index[Generation]},
	BlockName        -> YUKR2LR,
    	Value            -> {Y20LR[1,1] -> 0.0, Y20LR[1,2] -> 0.0, Y20LR[1,3] -> 0.0,
                      	     Y20LR[2,1] -> 0.0, Y20LR[2,2] -> 0.0, Y20LR[2,3] -> 0.0,
                             Y20LR[3,1] -> 0.0, Y20LR[3,2] -> 0.0, Y20LR[3,3] -> 0.0},
    	TeX              -> Superscript[Subscript[Y, "2"],LR],
	InteractionOrder -> {QLQ, 1},
    	Description      -> "r20 leptoquark LR Yukawa coupling matrix"
}
};

M$ClassesDescription = {
S[300] == {
    ClassName        -> r205,
    Mass             -> {Mr205, 1000},
    Width            -> {Wr205, 10},
    SelfConjugate    -> False,
    PropagatorLabel  -> "r205",
    PropagatorType   -> D,
    PropagatorArrow  -> None,
    QuantumNumbers   -> {Q -> 5/3, LeptonNumber -> -1},
    Indices          -> {Index[Colour]},
    PDG		     -> 4200025,
    ParticleName     -> "r205",
    AntiParticleName -> "r205~",
    FullName         -> "r205"
},

S[301] == {
    ClassName        -> r202,
    Mass             -> {Mr202, 1000},
    Width            -> {Wr202, 10},
    SelfConjugate    -> False,
    PropagatorLabel  -> "r202",
    PropagatorType   -> D,
    PropagatorArrow  -> None,
    QuantumNumbers   -> {Q -> 2/3, LeptonNumber -> -1},
    Indices          -> {Index[Colour]},
    PDG	      	     -> 4200022,
    ParticleName     -> "r202",
    AntiParticleName -> "r202~",
    FullName         -> "r202"
},

S[303] == {
    ClassName      -> r20,
    Unphysical     -> True,
    Indices        -> {Index[SU2D], Index[Colour]},
    FlavorIndex    -> SU2D,
    SelfConjugate  -> False,
    QuantumNumbers -> {Y -> 7/6},
    Definitions    -> { r20[1,cc_] :> r205[cc], r20[2,cc_] :> r202[cc]}
}
};

(* ************************** *)
(* *****  Lagrangian    ***** *)
(* ************************** *)

Lr2Kin := Module[ {mu,a,aa}, ExpandIndices[DC[r20bar[a,aa],mu] DC[r20[a,aa],mu], FlavorExpand->{SU2W,SU2D}]];

Lr2Yuk :=  - Y20RL[a,b] (uqbar[i,a,ss] ProjM[i,j] l[j,b] r205[ss]- uqbar[i,a,ss] ProjM[i,j] vl[j,b] r202[ss]) + Y20LR[a,b] (lbar[i,a] ProjM[i,j] CKM[c,b] uq[j,c,ss] r205bar[ss] + lbar[i,a] ProjM[i,j] dq[j,b,ss] r202bar[ss]);

LBSM := Lr2Kin + Lr2Yuk + HC[Lr2Yuk];

