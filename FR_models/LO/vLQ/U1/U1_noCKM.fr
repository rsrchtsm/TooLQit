(***************************************************************************************************************)
(******                                FeynRules model file for U1 Leptoquark                             ******)
(******                                              No-CKM                                               ******)
(******                                        * Version 1.0.0 *                                          ******)
(******                                                                                                   ******)
(******                        Authors: (alphabetically) Arvind Bhaskar, Arijit Das,                      ******)
(******                                 Atirek Kumar, Rachit Sharma, Subhadip Mitra,                      ******)
(******                                 Tanumoy Mandal, and Yash Chaurasia.                               ******)
(******                                                                                                   ******)
(******                                See README.md for citation information.                            ******)
(******                                                                                                   ******)
(***************************************************************************************************************)

(* ************************** *)
(* *****  Information   ***** *)
(* ************************** *)

M$ModelName = "U1_noCKM";

IndexRange[Index[SU2S]] = Unfold[Range[1]]; 
IndexStyle[SU2S,i];

M$InteractionOrderHierarchy = {
  {QCD, 1},
  {QED, 2},
  {QLQ, 3}
};

M$Parameters = {
X10LL == {
    	ParameterType    -> External,
	ComplexParameter -> False,
    	Indices          -> {Index[Generation], Index[Generation]},
	BlockName        -> YUKU1LL,
    	Value            -> {X10LL[1,1] -> 0.0, X10LL[1,2] -> 0.0, X10LL[1,3] -> 0.0,
                      	     X10LL[2,1] -> 0.0, X10LL[2,2] -> 0.0, X10LL[2,3] -> 0.0,
                             X10LL[3,1] -> 0.0, X10LL[3,2] -> 0.0, X10LL[3,3] -> 0.0},
    	TeX              -> Superscript[Subscript[x,10],LL],
	InteractionOrder -> {QLQ, 1},
    	Description      -> "u10 leptoquark LL Yukawa coupling matrix"
},

X10RR == {
    	ParameterType    -> External,
	ComplexParameter -> False,
    	Indices          -> {Index[Generation], Index[Generation]},
	BlockName        -> YUKU1RR,
    	Value            -> {X10RR[1,1] -> 0.0, X10RR[1,2] -> 0.0, X10RR[1,3] -> 0.0,
                      	     X10RR[2,1] -> 0.0, X10RR[2,2] -> 0.0, X10RR[2,3] -> 0.0,
                             X10RR[3,1] -> 0.0, X10RR[3,2] -> 0.0, X10RR[3,3] -> 0.0},
    	TeX              -> Superscript[Subscript[x,10],RR],
	InteractionOrder -> {QLQ, 1},
    	Description      -> "u10 leptoquark RR Yukawa coupling matrix"
},

kappa == {
   ParameterType    -> External,
   BlockName 	    -> kappa,
   OrderBlock       -> 1, 
   Value 	    -> 0,
   TeX 	            -> kp,   
   Description      -> "kappa"
}
};

M$ClassesDescription = {
V[100] == {
    ClassName        -> u102,
    SelfConjugate    -> False,
    Indices          -> {Index[Colour]},
    Mass             -> {Mu102, 1000},
    Width            -> {Wu102, 10},
    QuantumNumbers   -> {Q -> 2/3, LeptonNumber -> -1},
    PropagatorLabel  -> "u102",
    PropagatorType   -> Sine,
    PropagatorArrow  -> Forward,
    PDG              -> 7200011,
    ParticleName     -> "u102",
    AntiParticleName -> "u102~",
    FullName         -> "u102"
  },
  
V[110] == {
    ClassName      -> u10,
    Unphysical     -> True,
    Indices        -> {Index[SU2S], Index[Colour]},
    FlavorIndex    -> SU2S,
    SelfConjugate  -> False,
    QuantumNumbers -> {Y -> 2/3},
    Definitions    -> {u10[mu_,1,cc_] :> u102[mu,cc]}
}
};

(* ************************** *)
(* *****   Lagrangian   ***** *)
(* ************************** *)

Lu1Kin := Block[{mu,nu,a,aa,bb,cc}, ExpandIndices[-(1/2)(DC[u10bar[nu,a,aa],mu] - DC[u10bar[mu,a,aa],nu]) (DC[u10[nu,a,aa],mu] - DC[u10[mu,a,aa],nu]) - I gs (1.0 - kappa) u102bar[mu,aa] FS[G,mu,nu,cc] T[cc,aa,bb] u102[nu,bb]]];
  
Lu1int := X10LL[i,j] u102[mu,a] (dqbar[p,i,a] Ga[mu,p,q] ProjM[q,r] l[r,j] + uqbar[p,i,a] Ga[mu,p,q] ProjM[q,r] vl[r,j]) + X10RR[i,j] u102[mu,a] (dqbar[p,i,a] Ga[mu,p,q] ProjP[q,r] l[r,j]);

LBSM := Lu1Kin + Lu1int + HC[Lu1int];

