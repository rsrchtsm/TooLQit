(***************************************************************************************************************)
(******                                FeynRules model file for U3 Leptoquark                             ******)
(******                                              No-CKM                                               ******)
(******                                        * Version 1.0.0 *                                          ******)
(******                                                                                                   ******)
(******                        Authors: (alphabetically) Arvind Bhaskar, Arijit Das,                      ******)
(******                                 Atirek Kumar, Rachit Sharma, Subhadip Mitra,                      ******)
(******                                 Tanumoy Mandal, and Yash Chaurasia.                               ******)
(******                                                                                                   ******)
(******                                See README.md for citation information.                            ******)
(******                                                                                                   ******)
(***************************************************************************************************************)

(* ************************** *)
(* *****  Information   ***** *)
(* ************************** *)

M$ModelName = "U3_noCKM";

M$InteractionOrderHierarchy = {
  {QCD, 1},
  {QED, 2},
  {QLQ, 3}
};

M$Parameters = {
X30LL == {
    	ParameterType    -> External,
	ComplexParameter -> True,
    	Indices          -> {Index[Generation], Index[Generation]},
	BlockName        -> YUKU3LL,
    	Value            -> {X30LL[1,1] -> 0.0, X30LL[1,2] -> 0.0, X30LL[1,3] -> 0.0,
                      	     X30LL[2,1] -> 0.0, X30LL[2,2] -> 0.0, X30LL[2,3] -> 0.0,
                             X30LL[3,1] -> 0.0, X30LL[3,2] -> 0.0, X30LL[3,3] -> 0.0},
    	TeX              -> Superscript[Subscript[X, 30],LL],
	InteractionOrder -> {QLQ, 1},
    	Description      -> "u30 leptoquark LL Yukawa coupling matrix"
},

kappa == {
   ParameterType    -> External,
   BlockName 	    -> kappa,
   OrderBlock       -> 1, 
   Value 	    -> 0,
   TeX 	            -> kp,   
   Description      -> "kappa"
} 
};

M$ClassesDescription = {
V[801] == {
           ClassName        -> u301,
           Mass             -> {Mu301, 1000},
           Width            -> {Wu301, 10},
           SelfConjugate    -> False,
           PropagatorLabel  -> "u301",
           PropagatorType   -> Sine,
           PropagatorArrow  -> Forward,
           QuantumNumbers   -> {Q -> -1/3, LeptonNumber -> -1},
           Indices          -> {Index[Colour]},
           PDG	      	    -> 7200031,
           ParticleName     -> "u301",
           AntiParticleName -> "u301~",
           FullName         -> "u301"
},

V[802] == {
           ClassName        -> u302,
           Mass             -> {Mu302, 1000},
           Width            -> {Wu302, 10},
           SelfConjugate    -> False,
           PropagatorLabel  -> "u302",
           PropagatorType   -> Sine,
           PropagatorArrow  -> Forward,
           QuantumNumbers   -> {Q -> 2/3, LeptonNumber -> -1},
           Indices          -> {Index[Colour]},
           PDG	      	    -> 7200032,
           ParticleName     -> "u302",
           AntiParticleName -> "u302~",
           FullName         -> "u302"
},

V[805] == {
           ClassName        -> u305,
           Mass             -> {Mu305, 1000},
           Width            -> {Wu305, 10},
           SelfConjugate    -> False,
           PropagatorLabel  -> "u305",
           PropagatorType   -> Sine,
           PropagatorArrow  -> Forward,
           QuantumNumbers   -> {Q -> 5/3, LeptonNumber -> -1},
           Indices          -> {Index[Colour]},
           PDG	      	    -> 7200035,
           ParticleName     -> "u305",
           AntiParticleName -> "u305~",
           FullName         -> "u305"
},

V[807] == {
           ClassName      -> u30,
           Unphysical     -> True,
           SelfConjugate  -> False,
           QuantumNumbers -> {Y -> 2/3},
           Indices        -> {Index[SU2W], Index[Colour]},
           FlavorIndex    -> SU2W,
           Definitions    -> {u30[mu_,1,cc_] -> (u301[mu,cc] + u305[mu,cc])/Sqrt[2],
                              u30[mu_,2,cc_] -> (u301[mu,cc] - u305[mu,cc])/(I*Sqrt[2]),
                              u30[mu_,3,cc_] -> u302[mu,cc]}
}
};

(* ************************** *)
(* *****  Lagrangian    ***** *)
(* ************************** *)

Lu3Kin :=  Block[{mu,nu,a,aa,bb,cc}, ExpandIndices[-(1/2)(DC[u30bar[nu,a,aa],mu] - DC[u30bar[mu,a,aa],nu]) (DC[u30[nu,a,aa],mu] - DC[u30[mu,a,aa],nu]) - I gs kappa u30bar[mu,a,aa] FS[G,mu,nu,cc] T[cc,aa,bb] u30[nu,a,bb]]];

Lu3int :=  -X30LL[a,b] ((dqbar[i,a,ss] Ga[mu,i,j] ProjM[j,k] l[k,b] - uqbar[i,a,ss] Ga[mu,i,j] ProjM[j,k] vl[k,b]) u302[mu,ss] - 2^(0.5) (uqbar[i,a,ss] Ga[mu,i,j] ProjM[j,k] l[k,b] u305[mu,ss] + dqbar[i,a,ss] Ga[mu,i,j] ProjM[j,k] vl[k,b] u301[mu,ss]));
  
LBSM := Lu3Kin + Lu3int + HC[Lu3int];

